# ------------------------------------------------------------------------------
# ------------------------------------------------------ form linear combination
# ------------------------------------------------------------------------------
raw"""
[ entries for L_show ] = form_linear_combination(s, Xh)
"""
function form_linear_combination(s, Xh)
    k    = length(s)
    expr = Vector{Any}()

    for i in 1:k
        push!(expr, s[i])
        push!(expr, Xh[:, i])
        if i < k  # Add "+" only if it's not the last term
            push!(expr, "+")
        end
    end

    return expr
end

# ==============================================================================================================

raw"""pb = ShowGE{T}(A::AbstractMatrix{T}; output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{T}(A::AbstractMatrix{T}; tmp_dir="/tmp/la/run", output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{T}(A::AbstractMatrix{T}, B::AbstractVector{T}; output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{T}(A::AbstractMatrix{T}, B::AbstractMatrix{T}; output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{T}(A::AbstractMatrix{T}, (B1, B2, ...); output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{Rational{T}}(A::AbstractMatrix{T}, B::AbstractMatrix{T}; output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{Rational{T}}(A::AbstractMatrix{T}, B::AbstractVector{T}; output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{Rational{T}}(A::AbstractMatrix{T}, (B1, B2, ...); output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{Complex{Rational{T}}}(A::AbstractMatrix{Complex{T}}; output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{Rational{T}}(A::AbstractMatrix{T}; output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{Complex{Rational{T}}}(A::AbstractMatrix{Complex{T}}, B::AbstractVector{Complex{T}}; output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{Complex{Rational{T}}}(A::AbstractMatrix{Complex{T}}, B::AbstractMatrix{Complex{T}}; output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number
  <br>pb = ShowGE{Complex{Rational{T}}}(A::AbstractMatrix{Complex{T}}, (B1, B2, ...); output_dir="/tmp/la/run", keep_file="/tmp/la/run/show\\_layout") where T <: Number"""
mutable struct ShowGE{T<:Number}
    tmp_dir
    keep_file
    A
    B
    n_rhs
    normal_eq::Bool

    matrices
    cascade
    pivot_cols
    free_cols
    desc
    pivot_locs
    decorations
    rowechelon_paths
    variable_summary
    rank
    h
    xp
    xh
    status
    rhs_status
    rhs_consistent

    function ShowGE(A::AbstractMatrix; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout")
        ShowGE{eltype(A)}(A; tmp_dir=tmp_dir, output_dir=output_dir, keep_file=keep_file)
    end
    function ShowGE(A::AbstractMatrix, b; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout")
        ShowGE{eltype(A)}(A, b; tmp_dir=tmp_dir, output_dir=output_dir, keep_file=keep_file)
    end
    function ShowGE{T}(A::AbstractMatrix{T}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, A, nothing, Int[], false)
    end
    function ShowGE{T}(A::AbstractMatrix{T}, B::AbstractVector{T}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        Bm = reshape(B, :, 1)
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, A, (Bm,), [1], false)
    end
    function ShowGE{T}(A::AbstractMatrix{T}, B::AbstractMatrix{T}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, A, (B,), [size(B, 2)], false)
    end
    function ShowGE{T}(A::AbstractMatrix{T}, Bs::Tuple; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        blocks = _normalize_rhs_blocks(Bs)
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, A, blocks, _rhs_group_sizes(blocks), false)
    end
    function ShowGE{T}(A::AbstractMatrix{T}, Bs::AbstractVector{<:AbstractMatrix{T}}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        blocks = _normalize_rhs_blocks(Bs)
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, A, blocks, _rhs_group_sizes(blocks), false)
    end

    function ShowGE{Rational{T}}(A::AbstractMatrix{T}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, Rational{T}.(A), nothing, Int[], false)
    end
    function ShowGE{Rational{T}}(A::AbstractMatrix{T}, B::AbstractVector{T}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        Bm = Rational{T}.(reshape(B, :, 1))
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, Rational{T}.(A), (Bm,), [1], false)
    end
    function ShowGE{Rational{T}}(A::AbstractMatrix{T}, B::AbstractMatrix{T}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        Bm = Rational{T}.(B)
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, Rational{T}.(A), (Bm,), [size(Bm, 2)], false)
    end
    function ShowGE{Rational{T}}(A::AbstractMatrix{T}, Bs::Tuple; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        A2 = Rational{T}.(A)
        blocks = _normalize_rhs_blocks(map(B -> Rational{T}.(B), Bs))
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, A2, blocks, _rhs_group_sizes(blocks), false)
    end
    function ShowGE{Rational{T}}(A::AbstractMatrix{T}, Bs::AbstractVector{<:AbstractMatrix{T}}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        A2 = Rational{T}.(A)
        blocks = _normalize_rhs_blocks(map(B -> Rational{T}.(B), Bs))
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, A2, blocks, _rhs_group_sizes(blocks), false)
    end

    function ShowGE{Complex{Rational{T}}}(A::AbstractMatrix{Complex{T}}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, Complex{Rational{T}}.(A), nothing, Int[], false)
    end
    function ShowGE{Complex{Rational{T}}}(A::AbstractMatrix{Complex{T}}, B::AbstractVector{Complex{T}}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        Bm = Complex{Rational{T}}.(reshape(B, :, 1))
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, Complex{Rational{T}}.(A), (Bm,), [1], false)
    end
    function ShowGE{Complex{Rational{T}}}(A::AbstractMatrix{Complex{T}}, B::AbstractMatrix{Complex{T}}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        Bm = Complex{Rational{T}}.(B)
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, Complex{Rational{T}}.(A), (Bm,), [size(Bm, 2)], false)
    end
    function ShowGE{Complex{Rational{T}}}(A::AbstractMatrix{Complex{T}}, Bs::Tuple; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        A2 = Complex{Rational{T}}.(A)
        blocks = _normalize_rhs_blocks(map(B -> Complex{Rational{T}}.(B), Bs))
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, A2, blocks, _rhs_group_sizes(blocks), false)
    end
    function ShowGE{Complex{Rational{T}}}(A::AbstractMatrix{Complex{T}}, Bs::AbstractVector{<:AbstractMatrix{Complex{T}}}; tmp_dir="/tmp/la/run", output_dir=nothing, keep_file="/tmp/la/run/show_layout") where T <: Number
        A2 = Complex{Rational{T}}.(A)
        blocks = _normalize_rhs_blocks(map(B -> Complex{Rational{T}}.(B), Bs))
        new(_resolve_output_dir(output_dir, tmp_dir), keep_file, A2, blocks, _rhs_group_sizes(blocks), false)
    end
end

_normalize_rhs_blocks(Bs::Tuple) = tuple((_normalize_rhs_block(B) for B in Bs)...)
_normalize_rhs_blocks(Bs::AbstractVector{<:Union{AbstractMatrix,AbstractVector}}) = tuple((_normalize_rhs_block(B) for B in Bs)...)
_normalize_rhs_block(B::AbstractMatrix) = B
_normalize_rhs_block(B::AbstractVector) = reshape(B, :, 1)
_rhs_group_sizes(blocks::Tuple) = [size(B, 2) for B in blocks]

function _rhs_blocks(pb::ShowGE)
    pb.B === nothing && return ()
    return pb.B
end

_rhs_block_count(pb::ShowGE) = length(_rhs_blocks(pb))
_rhs_group_sizes(pb::ShowGE) = pb.B === nothing ? Int[] : pb.n_rhs
_rhs_col_count(pb::ShowGE) = sum(_rhs_group_sizes(pb))

function _validate_b_mat(pb::ShowGE, b_mat::Integer)
    n = _rhs_block_count(pb)
    if !(1 <= b_mat <= n)
        throw(ArgumentError("b_mat must satisfy 1 <= b_mat <= $n"))
    end
    return b_mat
end

function _validate_b_col(B::AbstractMatrix, b_col::Integer)
    n = size(B, 2)
    if !(1 <= b_col <= n)
        throw(ArgumentError("b_col must satisfy 1 <= b_col <= $n"))
    end
    return b_col
end

function _rhs_block_range(pb::ShowGE, b_mat::Integer)
    _validate_b_mat(pb, b_mat)
    sizes = _rhs_group_sizes(pb)
    start_col = 1 + sum(sizes[1:b_mat-1])
    stop_col = start_col + sizes[b_mat] - 1
    return start_col:stop_col
end

function _rhs_global_col_index(pb::ShowGE, b_mat::Integer, b_col::Integer)
    blk = _rhs_blocks(pb)[_validate_b_mat(pb, b_mat)]
    local_col = _validate_b_col(blk, b_col)
    return first(_rhs_block_range(pb, b_mat)) + local_col - 1
end

function _rhs_block_matrix(pb::ShowGE, b_mat::Integer)
    return _rhs_blocks(pb)[_validate_b_mat(pb, b_mat)]
end

function _combined_rhs_matrix(pb::ShowGE)
    blocks = _rhs_blocks(pb)
    isempty(blocks) && return nothing
    return hcat(blocks...)
end

# --------------------------------------------------------------------------------------------------------------
"""
    ref!(pb; gj=false, normal_eq=false)

Compute REF/RREF data for a `ShowGE` problem and attach pivot metadata.
"""
function ref!( pb::ShowGE{T}; gj::Bool=false, normal_eq::Bool=false )  where T <: Number
    M,N = size(pb.A)
    rhs = _combined_rhs_matrix(pb)
    if rhs !== nothing
        A = [pb.A rhs]
    else
        A = pb.A
    end
    if normal_eq
      pb.matrices, pb.pivot_cols, pb.desc = normal_eq_reduce_to_ref( A, n=N, gj=gj );
      sz = (N,N)
    else
      pb.matrices, pb.pivot_cols, pb.desc = reduce_to_ref( A, n=N, gj=gj );
      sz = (M,N)
    end
    pb.normal_eq = normal_eq
    pb.free_cols = filter(x -> !(x in pb.pivot_cols), 1:N)

    ge_dec = ge_decorations(pb.desc, pb.pivot_cols, sz; pivot_color="yellow!40")
    pb.pivot_locs = ge_dec.pivot_locs
    pb.decorations = ge_dec.decorations
    pb.rowechelon_paths = ge_dec.rowechelon_paths
    pb.variable_summary = ge_dec.variable_summary
    pb.rank = length( pb.pivot_cols )
    _compute_rhs_status!(pb)
    nothing
end
# --------------------------------------------------------------------------------------------------------------
function _rhs_val_to_tex(val)
    if val isa Rational
        return _rational_str(val)
    elseif val isa Complex{<:Rational}
        return _complex_rational_str(val)
    end
    return string(val)
end

function _format_rhs_label(rhs_labels::Vector{String})
    if isempty(rhs_labels)
        return ""
    end
    if length(rhs_labels) > 1
        return "\\left( " * join(rhs_labels, " \\mid ") * " \\right)"
    end
    return rhs_labels[1]
end

function _normal_eq_callouts(n_rows::Int, rhs_labels::Vector{String})
    callouts = Vector{Dict{String,Any}}()
    if n_rows <= 0
        return callouts
    end
    rhs0 = _format_rhs_label(rhs_labels)
    push!(callouts, Dict("grid" => (0, 1), "side" => "right", "label" => "\$\\mathbf{ $(rhs0) }\$", "color" => "blue"))
    if n_rows <= 1
        return callouts
    end
    rhs1_labels = ["A^T " * lbl for lbl in rhs_labels]
    rhs1 = _format_rhs_label(rhs1_labels)
    push!(callouts, Dict("grid" => (1, 0), "side" => "left", "label" => "\$\\mathbf{ A^T }\$", "color" => "blue"))
    push!(callouts, Dict("grid" => (1, 1), "side" => "right", "label" => "\$\\mathbf{ $(rhs1) }\$", "color" => "blue"))
    for i in 2:(n_rows - 1)
        prod = join(["E_$(k)" for k in (i - 1):-1:1], " ")
        rhs_i_labels = [prod == "" ? lbl : "$(prod) $(lbl)" for lbl in rhs1_labels]
        rhs_i = _format_rhs_label(rhs_i_labels)
        push!(callouts, Dict("grid" => (i, 0), "side" => "left", "label" => "\$\\mathbf{ E_$(i - 1) }\$", "color" => "blue"))
        push!(callouts, Dict("grid" => (i, 1), "side" => "right", "label" => "\$\\mathbf{ $(rhs_i) }\$", "color" => "blue"))
    end
    return callouts
end

function _inconsistent_rhs_value(pb::ShowGE{T}, global_b_col::Int) where T <: Number
    Ab = pb.matrices[end][end]
    n = size(pb.A, 2)
    m = size(Ab, 1)
    for i in 1:m
        row_zero = true
        for j in 1:n
            if Ab[i, j] != 0
                row_zero = false
                break
            end
        end
        if row_zero && Ab[i, n + global_b_col] != 0
            return Ab[i, n + global_b_col]
        end
    end
    return nothing
end

function _compute_rhs_status!(pb::ShowGE{T}) where T <: Number
    n_rhs = _rhs_col_count(pb)
    if n_rhs == 0
        pb.status = :none
        pb.rhs_status = Symbol[]
        pb.rhs_consistent = Bool[]
        return pb
    end
    Ab = pb.matrices[end][end]
    n = size(pb.A, 2)
    m = size(Ab, 1)
    rhs_status = Vector{Symbol}(undef, n_rhs)
    rhs_consistent = Vector{Bool}(undef, n_rhs)
    for k in 1:n_rhs
        status = :consistent
        for i in 1:m
            row_zero = true
            for j in 1:n
                if Ab[i, j] != 0
                    row_zero = false
                    break
                end
            end
            if row_zero && Ab[i, n + k] != 0
                status = :inconsistent
                break
            end
        end
        rhs_status[k] = status
        rhs_consistent[k] = (status == :consistent)
    end
    pb.rhs_status = rhs_status
    pb.rhs_consistent = rhs_consistent
    if all(s -> s == :consistent, rhs_status)
        pb.status = :consistent
    elseif all(s -> s == :inconsistent, rhs_status)
        pb.status = :inconsistent
    else
        pb.status = :mixed
    end
    return pb
end
# --------------------------------------------------------------------------------------------------------------
"""
    show_layout!(pb; array_names=nothing, show_variables=true, fig_scale=1, output_dir=nothing, output_stem=nothing)

Render the GE layout for a `ShowGE` problem as SVG.
Use `output_dir` and `output_stem` to retain TeX/PDF/SVG artifacts.
"""
function _keep_file_output_parts(keep_file)
    if keep_file === nothing
        return nothing, nothing
    end
    path = String(keep_file)
    dir = dirname(path)
    stem = splitext(basename(path))[1]
    return dir, stem
end

function _resolve_ge_output_targets(pb::ShowGE, output_dir, output_stem)
    keep_dir, keep_stem = _keep_file_output_parts(pb.keep_file)
    resolved_output_dir = output_dir !== nothing ? output_dir : (keep_dir !== nothing ? keep_dir : pb.tmp_dir)
    resolved_output_stem = output_stem !== nothing ? output_stem : keep_stem
    return resolved_output_dir, resolved_output_stem
end

function show_layout!(  pb::ShowGE{T}; array_names=nothing, show_variables=true, fig_scale=1, output_dir=nothing, output_stem=nothing, render_opts=nothing )   where T <: Number
    n_rhs = _rhs_col_count(pb)
    rhs_groups = _rhs_group_sizes(pb)
    if array_names === nothing
        if n_rhs == 0
            array_names = ["E", "A"]
        elseif n_rhs == 1
            array_names = ["E", ["A", "b"]]
        else
            array_names = ["E", ["A", "B"]]
        end
    end
    if pb.normal_eq
        callouts = nothing
        if !(array_names isa AbstractDict)
            rhs_labels = String[]
            try
                _, rhs = array_names
                rhs_labels = [string(x) for x in rhs]
            catch
                rhs_labels = ["A"]
            end
            callouts = _normal_eq_callouts(length(pb.matrices), rhs_labels)
            array_names = nothing
        end
            rhs_status = isdefined(pb, :rhs_status) ? [string(s) for s in pb.rhs_status] : nothing
            resolved_output_dir, resolved_output_stem = _resolve_ge_output_targets(pb, output_dir, output_stem)
            svg = matrixlayout_ge(
                pb.matrices;
                n_rhs=rhs_groups,
                pivot_locs=pb.pivot_locs,
                decorations=pb.decorations,
                rowechelon_paths=pb.rowechelon_paths,
                variable_summary=show_variables ? pb.variable_summary : nothing,
                variable_colors=["red", "black"],
                rhs_status=rhs_status,
                array_names=array_names,
                callouts=callouts,
                fig_scale=fig_scale,
                output_dir=resolved_output_dir,
                output_stem=resolved_output_stem,
                render_opts=render_opts,
            )
        pb.h = svg
        return svg
    end
    if isdefined(pb, :matrices) && pb.matrices !== nothing && length(pb.matrices) > 1
            rhs_status = isdefined(pb, :rhs_status) ? [string(s) for s in pb.rhs_status] : nothing
            resolved_output_dir, resolved_output_stem = _resolve_ge_output_targets(pb, output_dir, output_stem)
            svg = matrixlayout_ge(
                pb.matrices;
                n_rhs=rhs_groups,
                pivot_locs=pb.pivot_locs,
                decorations=pb.decorations,
                rowechelon_paths=pb.rowechelon_paths,
                variable_summary=show_variables ? pb.variable_summary : nothing,
                variable_colors=["red", "black"],
                rhs_status=rhs_status,
                array_names=array_names,
                fig_scale=fig_scale,
                output_dir=resolved_output_dir,
                output_stem=resolved_output_stem,
                render_opts=render_opts,
            )
        pb.h = svg
        return svg
    end
    la = load_LAFigureSpecs()
    rhs = _combined_rhs_matrix(pb)
    ge_svg = _pygetattr(la, :ge_svg)
    rhs_status = isdefined(pb, :rhs_status) ? pb.rhs_status : Symbol[]
    rhs_status_str = [string(s) for s in rhs_status]
    resolved_output_dir, resolved_output_stem = _resolve_ge_output_targets(pb, output_dir, output_stem)
    pb.h = _pycall(ge_svg, pb.A, rhs;
        show_pivots=true,
        fig_scale=fig_scale,
        variable_summary=show_variables && isdefined(pb, :variable_summary) ? pb.variable_summary : nothing,
        variable_colors=["red", "black"],
        rhs_status=rhs_status_str,
        array_names=array_names,
        output_dir=resolved_output_dir,
        render_opts=render_opts,
    )
    _ensure_pythoncall()
    svg_str = Base.invokelatest(PythonCall.pyconvert, String, pb.h)
    svg = SVGOut(svg_str)
    pb.h = svg
    return svg
end

include("GEWorkflowDisplay.jl")
# ==============================================================================================================
"""
    solutions(pb; b_mat=1, b_col=nothing) -> xp, xh

Return the particular and homogeneous solutions for one selected RHS matrix.
When `b_col` is omitted, `xp` contains one column per RHS column in the
selected matrix, preserving column order. When `b_col` is specified, `xp` is a
single solution vector for that RHS column. Inconsistent RHS columns produce a
zero particular solution vector and still preserve their position.
"""
function solutions(pb::ShowGE{T}; b_mat::Integer=1, b_col=nothing) where T <: Number
    _, N = size(pb.A)
    matrices, pivot_cols, _ = reduce_to_ref(pb.matrices[end][end][1:pb.rank, 1:end], n=N, gj=true)
    Xh = if length(pb.free_cols) > 0
        Xh0 = zeros(T, N, N - pb.rank)
        Fh = matrices[end][end][1:pb.rank, pb.free_cols]
        for (col, row) in enumerate(pb.free_cols)
            Xh0[row, col] = 1
        end
        Xh0[pivot_cols, :] = -Fh
        Xh0
    else
        zeros(T, N, 1)
    end
    if size(Xh, 2) == 0 || all(x -> x == 0, Xh)
        Xh = zeros(T, N, 1)
    end

    if _rhs_col_count(pb) > 0
        blk = _rhs_block_matrix(pb, b_mat)
        local_cols = b_col === nothing ? collect(1:size(blk, 2)) : [_validate_b_col(blk, Int(b_col))]
        Xp = zeros(T, N, length(local_cols))
        rhs_consistent = isdefined(pb, :rhs_consistent) ? pb.rhs_consistent : Bool[]
        F = matrices[end][end][1:pb.rank, N+1:end]
        for (j, local_col) in enumerate(local_cols)
            global_col = _rhs_global_col_index(pb, b_mat, local_col)
            if isempty(rhs_consistent) || rhs_consistent[global_col]
                Xp[pivot_cols, j] = F[:, global_col]
            end
        end
        if b_col !== nothing
            Xp = vec(Xp)
        end
    else
        Xp = b_col === nothing ? zeros(T, N, 1) : zeros(T, N)
    end

    if b_col === nothing
        pb.xp = Xp
        pb.xh = Xh
        return Xp, Xh
    end

    pb.xp = reshape(Xp, :, 1)
    pb.xh = Xh
    return Xp, Xh
end
# ------------------------------------------------------------------------------------------
function solve!(pb::ShowGE{T}; kwargs...) where T <: Number
    pb.xp, pb.xh = solutions(pb; kwargs...)
end
# ==============================================================================================================
"""
    lhs_matrix(pb; step=:final)

Return the coefficient matrix from a GE stack at the selected step.
"""
function lhs_matrix(pb::ShowGE{T}; step=:final) where T <: Number
    if !isdefined(pb, :matrices) || pb.matrices === nothing
        return pb.A
    end
    mats = pb.matrices
    idx = step === :final ? length(mats) : Int(step)
    Ab = mats[idx][end]
    return Ab[:, 1:size(pb.A, 2)]
end

"""
    rhs_matrix(pb, b_mat=1; step=:final)

Return the selected RHS matrix from a GE stack at the selected step.
"""
function rhs_matrix(pb::ShowGE{T}, b_mat::Integer=1; step=:final) where T <: Number
    _rhs_col_count(pb) == 0 && return nothing
    if !isdefined(pb, :matrices) || pb.matrices === nothing
        return _rhs_block_matrix(pb, b_mat)
    end
    mats = pb.matrices
    idx = step === :final ? length(mats) : Int(step)
    Ab = mats[idx][end]
    n = size(pb.A, 2)
    rng = _rhs_block_range(pb, b_mat)
    return Ab[:, n .+ collect(rng)]
end

"""
    rhs_column(pb, b_mat=1, b_col=1; step=:final)

Return one selected RHS column from a GE stack at the selected step.
"""
function rhs_column(pb::ShowGE{T}, b_mat::Integer=1, b_col::Integer=1; step=:final) where T <: Number
    rhs = rhs_matrix(pb, b_mat; step=step)
    rhs === nothing && return nothing
    _validate_b_col(rhs, b_col)
    return rhs[:, b_col]
end

# ==============================================================================================================
# function column_view( Xp, Xh, pivot_cols, rhs )
# end
# ==============================================================================================================
#function homogeneous_solution(pb::ShowGE{Complex{Rational{T}}}; b_col=1 )   where T <: Number)
#  N = size(pb.A,2)
#  matrices, pivot_cols, desc = reduce_to_ref( pb.matrices[end][end][:,1:N], n=N, gj=true );
#  Xh = similar(pb.A, size(pb.A,1), A - pb.rank)
#end
# ==============================================================================================================
raw"""function show_ge_final( matrices, desc, pivot_cols; n_rhs=0, formatter=to_latex, pivot_list=nothing, bg_for_entries=nothing, <br>
             variable_colors=["blue","black"], pivot_colors=["blue","yellow!40"],  <br>
             ref_path_list=nothing, comment_list=[], variable_summary=nothing, array_names=nothing, <br>
             start_index=1, func=nothing, fig_scale=nothing, output_dir=nothing, output_stem=nothing, tmp_dir=nothing )
 """
function julia_ge( matrices, desc, pivot_cols; n_rhs=0, formatter=to_latex, pivot_list=nothing, bg_for_entries=nothing,
             variable_colors=["blue","black"], pivot_colors=["blue","yellow!40"],
             ref_path_list=nothing, comment_list=[], variable_summary=nothing, array_names=nothing,
             start_index=1, func=nothing, fig_scale=nothing, output_dir=nothing, output_stem=nothing, tmp_dir=nothing,
             render_opts=nothing )
    Ab = matrices[end][end]
    nrhs = n_rhs isa AbstractArray ? sum(n_rhs) : n_rhs
    if nrhs > 0
        A = Ab[:, 1:(end - nrhs)]
        rhs = Ab[:, (end - nrhs + 1):end]
    else
        A = Ab
        rhs = nothing
    end
    la = load_LAFigureSpecs()
    ge_svg = _pygetattr(la, :ge_svg)
    local_render_opts = render_opts === nothing ? Dict{String, Any}() : Dict{String, Any}(render_opts)
    resolved_output_dir = output_dir !== nothing ? output_dir : tmp_dir
    resolved_output_stem = output_stem
    if !haskey(local_render_opts, "output_dir") && !haskey(local_render_opts, :output_dir)
        if output_dir !== nothing
            local_render_opts["output_dir"] = resolved_output_dir
        elseif tmp_dir !== nothing
            local_render_opts["output_dir"] = mktempdir(resolved_output_dir)
        end
    end
    call_kwargs = Dict{Symbol, Any}(
        :fig_scale => fig_scale,
        :array_names => array_names,
        :variable_summary => variable_summary,
        :variable_colors => variable_colors,
        :render_opts => local_render_opts,
    )
    if output_dir !== nothing
        call_kwargs[:output_dir] = resolved_output_dir
    end
    if resolved_output_stem !== nothing
        call_kwargs[:output_stem] = resolved_output_stem
    end
    s = _pycall(ge_svg, A, rhs; call_kwargs...)
    _ensure_pythoncall()
    return Base.invokelatest(PythonCall.pyconvert, String, s)
end
"""
    SVGOut(svg::String)

Wrapper type for SVG output from `show_ge_final`, enabling rich display in IJulia.
"""
struct SVGOut
    svg::String
end

import Base: show

"""
    show(io::IO, ::MIME"image/svg+xml", x::SVGOut)

Emit the SVG payload for rich notebook display.
"""
function show(io::IO, ::MIME"image/svg+xml", x::SVGOut)
    print(io, x.svg)
end
"""
    show_ge_final(args...; kwargs...) -> SVGOut

Render only the final GE table from `matrices[end][end]`.
"""
show_ge_final(args...; kwargs...) = SVGOut(julia_ge(args...; kwargs...))

function _matrices_are_strings(mats)
    for row in mats
        for cell in row
            if cell === nothing
                continue
            end
            if cell isa AbstractArray
                for v in cell
                    if v === nothing
                        continue
                    end
                    return v isa AbstractString
                end
            else
                return cell isa AbstractString
            end
        end
    end
    return false
end

function _ge_block_to_list(block)
    if block === nothing || block === :none
        return nothing
    end
    if block isa LinearAlgebra.Adjoint
        parent_block = parent(block)
        # For non-numeric entries (e.g., latex string cells), avoid element-wise
        # adjoint application and perform a plain transpose of the container.
        block = eltype(parent_block) <: Number ? Matrix(block) : permutedims(parent_block)
    elseif block isa LinearAlgebra.Transpose
        block = permutedims(parent(block))
    end
    if block isa AbstractArray
        rows = Vector{Any}()
        for i in axes(block, 1)
            row = Vector{Any}()
            for j in axes(block, 2)
                push!(row, block[i, j])
            end
            push!(rows, row)
        end
        return rows
    end
    return block
end

function _ge_grid_to_lists(mats)
    return [[_ge_block_to_list(block) for block in row] for row in mats]
end

function _ge_normalize_grid(mats)
    if mats isa AbstractVector
        if isempty(mats)
            return mats
        end
        first = mats[1]
        if first isa AbstractArray && !(first isa AbstractVector)
            return [[m] for m in mats]
        end
    end
    return mats
end

function _ge_to_pylist(obj)
    if obj isa AbstractDict
        _ensure_pythoncall()
        return Base.invokelatest(PythonCall.pydict, Dict(k => _ge_to_pylist(v) for (k, v) in obj))
    end
    if obj isa AbstractArray
        _ensure_pythoncall()
        return Base.invokelatest(PythonCall.pylist, [_ge_to_pylist(x) for x in obj])
    end
    return obj
end

function _normalize_bg_specs(specs)
    if !(specs isa AbstractVector)
        return [specs]
    end
    if isempty(specs)
        return specs
    end
    if length(specs) >= 3 && !(specs[1] isa AbstractVector) && !(specs[2] isa AbstractVector)
        return [specs]
    end
    if !all(x -> x isa AbstractVector, specs)
        return [specs]
    end
    return specs
end

function _matrix_shape_from_grid(mats_raw, gM::Int, gN::Int)
    if mats_raw === nothing
        return 0, 0
    end
    try
        grid = mats_raw[gM + 1]
        mat = grid[gN + 1]
        if mat === nothing || mat isa AbstractString
            return 0, 0
        elseif mat isa AbstractMatrix
            return size(mat)
        elseif mat isa AbstractArray && ndims(mat) == 2
            return size(mat)
        elseif mat isa AbstractVector
            nrows = length(mat)
            ncols = nrows == 0 ? 0 : length(mat[1])
            return nrows, ncols
        end
    catch
    end
    return 0, 0
end

function _bg_for_entries_to_decorators(bg_for_entries, mats_raw=nothing)
    if bg_for_entries === nothing
        return nothing
    end
    _ensure_pythoncall()
    fmt = _pyimport("matrixlayout.formatting")
    make_decorator = Base.invokelatest(PythonCall.pygetattr, fmt, "make_decorator")
    sel_entry = Base.invokelatest(PythonCall.pygetattr, fmt, "sel_entry")
    sel_box = Base.invokelatest(PythonCall.pygetattr, fmt, "sel_box")
    specs = _normalize_bg_specs(bg_for_entries)
    decorators = Vector{Any}()
    for spec in specs
        if !(spec isa AbstractVector) || length(spec) < 3
            continue
        end
        gM = Int(spec[1])
        gN = Int(spec[2])
        nrows, ncols = _matrix_shape_from_grid(mats_raw, gM, gN)
        entries = spec[3]
        color = length(spec) >= 4 ? string(spec[4]) : "red!15"
        if !(entries isa AbstractVector)
            entries = [entries]
        end
        entry_selectors = Vector{Any}()
        decorator = Base.invokelatest(make_decorator; bg_color=color)
        for entry in entries
            if (entry isa AbstractVector || entry isa Tuple) && length(entry) == 2 &&
               (entry[1] isa AbstractVector || entry[1] isa Tuple) &&
               (entry[2] isa AbstractVector || entry[2] isa Tuple)
                i0, j0 = entry[1]
                i1, j1 = entry[2]
                push!(entry_selectors, Base.invokelatest(sel_box, (Int(i0), Int(j0)), (Int(i1), Int(j1))))
            else
                i0, j0 = entry
                push!(entry_selectors, Base.invokelatest(sel_entry, Int(i0), Int(j0)))
            end
        end
        push!(decorators, Dict(
            "grid" => (gM, gN),
            "entries" => entry_selectors,
            "decorator" => decorator,
        ))
    end
    return isempty(decorators) ? nothing : decorators
end

function _prepare_ge_mats(matrices, formatter)
    mats = matrices
    if !_matrices_are_strings(mats)
        mats = formatter(mats)
    end
    mats = _ge_normalize_grid(mats)
    return _ge_grid_to_lists(mats)
end

function _merge_bg_decorators(bg_for_entries, decorators, mats)
    decorators_from_bg = _bg_for_entries_to_decorators(bg_for_entries, mats)
    if decorators !== nothing && !(decorators isa AbstractVector)
        decorators = [decorators]
    end
    if decorators_from_bg !== nothing
        bg_for_entries = nothing
        decorators = decorators === nothing ? decorators_from_bg : vcat(decorators_from_bg, decorators)
    end
    return bg_for_entries, decorators
end

function _call_ge_convenience(mats_py; kwargs...)
    _ensure_pythoncall()
    la = load_LAFigureSpecs()
    ge_fn = _pygetattr(la, :ge_svg)
    return _pycall(ge_fn, mats_py; kwargs...)
end

function _ge_pyify_payload(
    mats, pivot_list, bg_for_entries, ref_path_list, comment_list,
    variable_summary, rhs_status, array_names, callouts, decorators, decorations,
    pivot_locs, rowechelon_paths
)
    return (
        mats=_ge_to_pylist(mats),
        pivot_list=_ge_to_pylist(pivot_list),
        bg_for_entries=_ge_to_pylist(bg_for_entries),
        ref_path_list=_ge_to_pylist(ref_path_list),
        comment_list=_ge_to_pylist(comment_list),
        variable_summary=_ge_to_pylist(variable_summary),
        rhs_status=_ge_to_pylist(rhs_status),
        array_names=_ge_to_pylist(array_names),
        callouts=_ge_to_pylist(callouts),
        decorators=_ge_to_pylist(decorators),
        decorations=_ge_to_pylist(decorations),
        pivot_locs=_ge_to_pylist(pivot_locs),
        rowechelon_paths=_ge_to_pylist(rowechelon_paths),
    )
end

function matrixlayout_ge( matrices; n_rhs=0, formatter=to_latex, pivot_list=nothing, bg_for_entries=nothing,
             variable_colors=["blue","black"], pivot_colors=["blue","yellow!40"], pivot_text_color=nothing,
             ref_path_list=nothing, comment_list=[], variable_summary=nothing, array_names=nothing,
             start_index=1, func=nothing, fig_scale=nothing, output_dir=nothing, output_stem=nothing, tmp_dir=nothing,
             render_opts=nothing, rhs_status=nothing, pivot_locs=nothing, rowechelon_paths=nothing, kwargs... )
    mats = _prepare_ge_mats(matrices, formatter)
    # Preserve legacy GE background highlights so pivot-column shading stays on the
    # original `bg_for_entries -> codebefore` render path used by GenLAProblems.
    if haskey(kwargs, :specs)
        throw(ArgumentError("Removed GE matrix-label alias: specs. Use callouts instead."))
    end
    callouts = get(kwargs, :callouts, nothing)
    decorators = get(kwargs, :decorators, nothing)
    decorations = get(kwargs, :decorations, nothing)
    # create_medium_nodes/create_extra_nodes are handled by LAFigureSpecs.ge_convenience
    payload = _ge_pyify_payload(
        mats, pivot_list, bg_for_entries, ref_path_list,
        comment_list, variable_summary, rhs_status, array_names,
        callouts, decorators, decorations, pivot_locs, rowechelon_paths
    )

    _ensure_pythoncall()
    builtins = _pyimport("builtins")
    py_str = Base.invokelatest(PythonCall.pygetattr, builtins, "str")
    nrhs_arg = n_rhs isa AbstractVector ? _ge_to_pylist(n_rhs) : n_rhs
    if pivot_text_color === nothing
        pivot_text_color = pivot_colors[1]
    end
    resolved_output_dir = output_dir !== nothing ? output_dir : tmp_dir
    svg = _call_ge_convenience(
        payload.mats;
        n_rhs=nrhs_arg,
        formatter=py_str,
        pivot_list=payload.pivot_list,
        pivot_locs=payload.pivot_locs,
        bg_for_entries=payload.bg_for_entries,
        variable_colors=variable_colors,
        pivot_text_color=pivot_text_color,
        ref_path_list=payload.ref_path_list,
        rowechelon_paths=payload.rowechelon_paths,
        comment_list=payload.comment_list,
        variable_summary=payload.variable_summary,
        rhs_status=payload.rhs_status,
        array_names=payload.array_names,
        callouts=payload.callouts,
        decorators=payload.decorators,
        decorations=payload.decorations,
        start_index=start_index,
        func=func,
        fig_scale=fig_scale,
        output_dir=resolved_output_dir,
        output_stem=output_stem,
        render_opts=render_opts,
    )
    svg_str = Base.invokelatest(PythonCall.pyconvert, String, svg)
    return SVGOut(svg_str)
end

# ------------------------------------------------------------------------------------------
"""
    show_solution(matrices; var_name="x", fig_scale=1, output_dir=nothing, tmp_dir=nothing, render_svg=true)

Render the standard solution form from the final augmented matrix.
"""
function show_solution( matrices; var_name::String="x", fig_scale=1, output_dir=nothing, tmp_dir=nothing, render_svg=true, render_opts=nothing )
    Ab = matrices[end][end]
    A = Ab[:, 1:(size(Ab, 2) - 1)]
    b = Ab[:, end]
    A = _python_exact_literal_if_needed(A)
    b = _python_exact_literal_if_needed(b)
    tex = load_LAFigureSpecs().standard_solution_tex(A, b, var_name=var_name)
    if render_svg
        return _render_solution_svg(tex; fig_scale=fig_scale, output_dir=_resolve_output_dir(output_dir, tmp_dir), render_opts=render_opts)
    end
    return _display_tex(tex)
end
